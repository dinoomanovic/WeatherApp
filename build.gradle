// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile file("../config/checkstyle/checkstyle.xml")
        configProperties = ["suppressionFile": project(':').file('config/checkstyle/suppressions.xml')]
        source 'src/main/java'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        // empty classpath
        classpath = files()
    }

    task pmd(type: Pmd) {
        description 'Identifying potential problems mainly dead code, duplicated code, cyclomatic complexity and overcomplicated expressions'
        group 'verification'
        ruleSetFiles = files("$project.rootDir/config/pmd/rules-pmd.xml")
        source = fileTree('src/main/java')
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    task findbugs(type: FindBugs) {
        description 'Find bugs mainly design flaws, bad practices, multithreaded correctness and code vulnerabilities.'
        group 'verification'
        excludeFilter = file("$project.rootDir/config/findbugs/rules-findbugs.xml")
        classes = fileTree("$project.buildDir/intermediates/classes/debug/com/odin")
        source = fileTree('src/main/java')
        effort 'max'
        reportLevel = "high"
        classpath = files()

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

ext {
    AAVersion = '4.5.1'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
